* Reads the minimized water box, defines a crystal, and heats.

BOMBlev 0

open read unit 10 card name ../top_all27_prot_na.inp
read RTF unit 10 card
close unit 10

open read unit 02 card name ../par_all27_prot_na.inp
read para unit 02 card
close unit 02

open read unit 12 card name min-box.pdb
read sequ pdb unit 12
generate TIP3 setup nodihedral noangle
rewind unit 12
!open read unit 12 card name min-box.pdb
read coor pdb unit 12
close unit 12

! If necessary, generate missing coordinates.
!ic fill pres
!ic parameters
!ic build

!coord init sele type h* end
!hbuild

! Define a crystal.
crystal defi ortho 34.0 34.0 39.0 90. 90. 90.
crystal build cutoff 15.0
image byres xcen 0.0 ycen 0.0 zcen 0.0 sele all end
Update inbfrq -1 imgfrq 40 ihbfrq 40 cutim 15.0

open write unit 1 card name heat-box.rst

! Heat.
! Keywords:
! start (restart): defines whether you start a new simulation or continue 
! from previous one.
! nstep: Number of timesteps  
! tstep: Timestep (0.001 = 1fs) 
! iprfrq ??: Print out results every ?? steps
! ihtfrq ??: Heat up system every ?? steps
! teminc ??: Heat in temperature increments of ?? K
! nprint ??: Print out average values every ?? steps
! iunwri 1: File unit number to write restart file to. = 1  
! iunrea -1: File unit numer to read restart file from. -1 = don't write 
! iuncrd -1: File unit numer to write trajectories to. -1 = don't write  
! nsavc 100: save coordinates to trajectory file every 100 steps
! is only active when writing trajectory file       
! firstt: Starting temperature of 0.0 K after minimisation
! finalt: Temperature you want to heat up to. Change 
! to your temperature
dyna verlet start nstep 20000 timestep 0.001 eche 50.0 -
    iprfrq 100 ihtfrq 100 teminc 5.0  -
    nprint 100 iunwri 1 iunrea -1 iuncrd -1 nsavc 100 -
    firstt 0.0 finalt 300.0 -
    iasors 0 iasvel 1 iscvel 0 ichecw 0 

! Save coordinates.
open write unit 1 card name heat-box.psf
write psf card unit 1
close unit 1

open write unit 1 card name heat-box.pdb
write coor pdb unit 1
close unit 1

stop
